plugins {
    id 'com.gladed.androidgitversion' version '0.3.3'
}
apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    androidGitVersion {
        codeFormat = 'PP'
    }

    defaultConfig {
        applicationId "com.intive.toz"
        resValue "string", "app_name", "TOZ"
        archivesBaseName = "toz"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode androidGitVersion.code()
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    productFlavors {
        dev {
            minSdkVersion 21
        }
        prod {
            minSdkVersion 16
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            resValue "string", "app_name", "TOZ - dev"
        }
    }
    lintOptions {
        abortOnError = false
    }

}

def versions = [
        support : "25.2.0",
        retrofit: "2.1.0"
]

def testVersions = [
        junit   : "4.12",
        espresso: "2.2.2"
]

dependencies {
    compile "com.android.support:appcompat-v7:$versions.support"

    compile "com.squareup.retrofit2:retrofit:$versions.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$versions.retrofit"


    testCompile "junit:junit:$testVersions.junit"

    androidTestCompile("com.android.support.test.espresso:espresso-core:$testVersions.espresso") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    compile 'com.hannesdorfmann.mosby3:mvp:3.0.0'
    compile 'com.jakewharton:butterknife:8.5.1'
    compile 'com.android.support:support-v4:25.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
}

checkstyle {
    toolVersion = '6.19'
}

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
}